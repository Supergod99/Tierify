println "[Echelon] forge/build.gradle loaded"

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net/" }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:6.0.46"
    }
}

apply plugin: "java"
apply plugin: "net.minecraftforge.gradle"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven { url = "https://maven.minecraftforge.net/" }
    maven {
        url "https://cursemaven.com/"
        content { includeGroup "curse.maven" }
    }
    maven { url = "https://maven.shadowsoffire.dev/releases" }
}


dependencies {
    minecraft "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
    implementation fg.deobf("curse.maven:placebo-283644:6274231")
    implementation fg.deobf("curse.maven:apothic-attributes-898963:5634071")
    runtimeOnly fg.deobf("curse.maven:curios-309927:6418456")
    runtimeOnly fg.deobf("curse.maven:armageddon-1110642:7142556")
    runtimeOnly fg.deobf("curse.maven:attributefix-280510:4911084")
    runtimeOnly fg.deobf("curse.maven:geckolib-388172:7025129")
    runtimeOnly fg.deobf("curse.maven:tooltipoverhaul-1327508:7096296")
    compileOnly "org.joml:joml:1.10.5"
    compileOnly "com.mojang:datafixerupper:6.0.8"
    compileOnly "com.mojang:brigadier:1.2.9"
}

tasks.named("jar", Jar).configure {
    // Tell Forges Mixin bootstrap which configs to load
    manifest {
        attributes([
            "MixinConfigs": "tiered.forge.mixins.json"
        ])
    }
}


minecraft {
    mappings channel: "official", version: rootProject.minecraft_version

    runs {
        client {
            // FORCE mixin configs in dev runs
            args '--mixin.config', 'tiered.forge.mixins.json'

            // Optional but very useful while validating
            property 'mixin.debug.verbose', 'true'
            property 'mixin.debug.export', 'true'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${project.buildDir}/createSrgToMcp/output.srg"
            workingDirectory project.file("run")

            mods {
                tiered {
                    source sourceSets.main
                }
            }
        }

        server {
            args '--mixin.config', 'tiered.forge.mixins.json'

            property 'mixin.debug.verbose', 'true'
            property 'mixin.debug.export', 'true'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${project.buildDir}/createSrgToMcp/output.srg"
            workingDirectory project.file("run-server")

            mods {
                tiered {
                    source sourceSets.main
                }
            }
        }
    }
}
